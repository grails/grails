GORM classes also support Hibernate's query language HQL, a very complete reference for which can be found [Chapter 14. HQL: The Hibernate Query Language|http://www.hibernate.org/hib_docs/reference/en/html/queryhql.html] of the Hibernate documentation. 
 
GORM provides a number of methods that work with HQL including [find|domainClasses], [findAll|domainClasses] and [executeQuery|domainClasses]. An example of a query can be seen below:

{code:java}
def results =
      Book.findAll("from Book as b where b.title like 'Lord of the%'")
{code}

h4. Positional and Named Parameters

In this case the value passed to the query is hard coded, however you can equally use positional parameters:

{code:java}
def results =
      Book.findAll("from Book as b where b.title like ?", ["The Shi%"])

{code}

{code:java}
def author = Author.findByName("Stephen King")
def books = Book.findAll("from Book as book where book.author = ?", [author])
{code}

Or even named parameters:

{code:java}
def results =
      Book.findAll("from Book as b where b.title like :search or b.author like :search", [search:"The Shi%"])
{code}

{code:java}
def author = Author.findByName("Stephen King")
def books = Book.findAll("from Book as book where book.author = :author", [author: author])
{code}

h4. Multiline Queries

If you need to separate the query across multiple lines you can use a line continuation character:

{code:java}
def results = Book.findAll("\\\
from Book as b, \\\
     Author as a \\\
where b.author = a and a.surname = ?", ['Smith'])
{code}

{note}
Groovy multiline strings will NOT work with HQL queries
{note}

h4. Pagination and Sorting

You can also perform pagination and sorting whilst using HQL queries. To do so simply specify the pagination options as a map at the end of the method call and include an "ORDER BY" clause in the HQL:

{code:java}
def results =
      Book.findAll("from Book as b where b.title like 'Lord of the%' order by b.title asc",
                   [max:10, offset:20])
{code}
