h1. createCriteria 

h2. Purpose

Creates an returns an instance of Grails' [HibernateCriteriaBuilder|api:grails.orm.HibernateCriteriaBuilder] that can be used to construct criteria queries

h2. Examples

{code:java}
def c = Account.createCriteria()
def results = c.list {
	like("holderFirstName", "Fred%")
	and {
		between("balance", 500, 1000)
		eq("branch", "London")
	}
	maxResults(10)
	order("holderLastName", "desc")
}
{code}

h2. Description

Criteria queries are a type safe, advanced way to query that uses a Groovy builder to construct potentially complex queries. It is a much better alternative to using StringBuffer.

Refer to the user guide section on [Criteria|guide:criteria] for usage instructions. Below is a node reference for each criterion method:

{table}
 Node | Description | Example    
 *between* | Where the property value is between to distinct values | {code}
between("balance", 500, 1000)
{code}       
 *eq* | Where a property equals a particular value | {code}
eq("branch", "London")
{code}       
 *eqProperty* | Where one property must equal another | {code}
eqProperty("lastTransaction","firstTransaction")
{code}       
 *gt* | Where a property is greater than a particular value | {code}
gt("balance",1000)
{code}       
 *gtProperty* | Where a one property must be greater than another | {code}
gtProperty("balance","overdraft")
{code}       
 *ge* | Where a property is greater than or equal to a particular value | {code}
ge("balance",1000)
{code}       
 *geProperty* | Where a one property must be greater than or equal to another | {code}
geProperty("balance","overdraft")
{code}       
 *idEq* | Where an objects id equals the specified value | {code}
idEq(1)
{code}       
 *ilike* | A case-insensitive 'like' expression | {code}
ilike("holderFirstName","Steph%")
{code}       
 *in* | Where a one property is contained within the specified list of values note: 'in' is a groovy reserve word, we must escape it by quotes. | {code}
'in'("holderAge",[18..65])
{code}       
 *isEmpty* | Where a collection property is empty | {code}
isEmpty("transactions")
{code}       
 *isNotEmpty* | Where a collection property is not empty | {code}
isNotEmpty("transactions")
{code}       
 *isNull* | Where a property is null | {code}
isNull("holderGender")
{code}       
 *isNotNull* | Where a property is not null | {code}
isNotNull("holderGender")
{code}       
 *lt* | Where a property is less than a particular value | {code}
lt("balance",1000)
{code}       
 *ltProperty* | Where a one property must be less than another | {code}
ltProperty("balance","overdraft")
{code}       
 *le* | Where a property is less than or equal to a particular value | {code}
le("balance",1000)
{code}       
 *leProperty* | Where a one property must be less than or equal to another | {code}
leProperty("balance","overdraft")
{code}       
 *like* | Equivalent to SQL like expression | {code}
like("holderFirstName","Steph%")
{code}       
 *ne* | Where a property does not equals a particular value | {code}
ne("branch", "London")
{code}       
 *neProperty* | Where one property does not equal another | {code}
neProperty("lastTransaction","firstTransaction")
{code}       
 *order* | Order the results by a particular property | {code}
order("holderLastName", "desc")
{code}       
 *rlike* | Similar to like, but uses a regex. Only supported on Oracle and MySQL. | {code}
rlike("holderFirstName",/Steph.+/)
{code}       

 *sizeEq* | Where a collection property's size equals a particular value | {code}
sizeEq("transactions", 10)
{code}       
 *sizeGt* | Where a collection property's size is greater than a particular value | {code}
sizeGt("transactions", 10)
{code}       
 *sizeGe* | Where a collection property's size is greater than or equal to a particular value | {code}
sizeGe("transactions", 10)
{code}       
 *sizeLt* | Where a collection property's size is less than a particular value | {code}
sizeLt("transactions", 10)
{code}       
 *sizeLe* | Where a collection property's size is less than or equal to a particular value | {code}
sizeLe("transactions", 10)
{code}       
 *sizeNe* | Where a collection property's size is not equal to a particular value | {code}
sizeNe("transactions", 10)
{code}
 *sqlRestriction* | Use arbitrary SQL to modify the resultset  | {code}
sqlRestriction "char_length( first_name ) == 4"
{code}
{table}

Criteria also support the notion of projections. A projection is used to change the nature of the results. For example the following query using a projection to count the number of distinct @branch@ names there are for each @Account@:

{code:java}
def c = Account.createCriteria()
def branchCount = c.get {
	projections {
		countDistinct "branch"
	}
}
{code}

The following table summarizes the different projections and what they do:

{table}
Name | Description | Example
*property* | Returns the given property in the returned results | {code:java}
property("firstName"){code}
*distinct* | Returns results using a single or collection of distinct property names | {code:java}
distinct("lastName") or distinct(['firstName', 'lastName']){code}
*avg* | Returns the average value of the given property | {code:java}
avg("age"){code}
*count* | Returns the count of the given property name | {code:java}
count("branch"){code}
*countDistinct* | Returns the distinct count of the given property name | {code:java}
countDistinct("branch"){code}
*groupProperty* | Groups the results by the given property | {code:java}
groupProperty("lastName"){code}
*max* | Returns the maximum value of the given property | {code:java}
max("age"){code}
*min* | Returns the minimum value of the given property | {code:java}
min("age"){code}
*sum* | Returns the sum of the given property | {code:java}
sum("balance"){code}
*rowCount* | Returns count of the number of rows returned | {code:java}
rowCount(){code}

{table}